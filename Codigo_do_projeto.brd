#include <Servo.h>
#include <Adafruit_LiquidCrystal.h>

Adafruit_LiquidCrystal lcd_1(0);
int pinoServo1 = 3;
int pinoServo2 = 13;
Servo servo1;
Servo servo2;
int led1 = 4;
int led2 = 12;
int trig1 = 7;
int echo1 = 6;
int trig2 = 10;
int echo2 = 9;
int buzzer = 2;
int distanciaEntrada;
int distanciaSaida;
int vagasDisponiveis = 10;
unsigned long tempoAberturaEntrada = 0;
unsigned long tempoAberturaSaida = 0;
bool cancelaAbertaEntrada = false;
bool cancelaAbertaSaida = false;
bool carroEntradaProcessado = false;
bool carroSaidaProcessado = false;
unsigned long TEMPO_ABERTURA = 3000;

void setup() {
  servo1.attach(pinoServo1, 500, 2500);
  servo2.attach(pinoServo2, 500, 2500);
  servo1.write(0);
  servo2.write(0);

  pinMode(led1, OUTPUT);
  pinMode(led2, OUTPUT);
  digitalWrite(led1, LOW);
  digitalWrite(led2, LOW);

  pinMode(trig1, OUTPUT);
  pinMode(echo1, INPUT);
  pinMode(trig2, OUTPUT);
  pinMode(echo2, INPUT);

  pinMode(buzzer, OUTPUT);

  lcd_1.begin(16, 2);
  lcd_1.print("Sistema ativado");
  delay(2000);
  lcd_1.clear();

  Serial.begin(9600);
}

long medirDistancia(int trigPin, int echoPin) {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  long duracao = pulseIn(echoPin, HIGH);
  return duracao / 58;
}

void controlarEntrada() {
  if (distanciaEntrada < 50 && !cancelaAbertaEntrada && !carroEntradaProcessado) {
    digitalWrite(led1, HIGH);
    tone(buzzer, 1500, 200);
    servo1.write(90);
    tempoAberturaEntrada = millis();
    cancelaAbertaEntrada = true;
    vagasDisponiveis--;
    carroEntradaProcessado = true;
  }
  if (cancelaAbertaEntrada && millis() - tempoAberturaEntrada >= TEMPO_ABERTURA) {
    digitalWrite(led1, LOW);
    noTone(buzzer);
    servo1.write(0);
    cancelaAbertaEntrada = false;
    carroEntradaProcessado = false;
  }
}

void controlarSaida() {
  if (distanciaSaida < 50 && !cancelaAbertaSaida && !carroSaidaProcessado) {
    digitalWrite(led2, HIGH);
    tone(buzzer, 2000, 200);
    servo2.write(90);
    tempoAberturaSaida = millis();
    cancelaAbertaSaida = true;
    vagasDisponiveis++;
    carroSaidaProcessado = true;
  }
  if (cancelaAbertaSaida && millis() - tempoAberturaSaida >= TEMPO_ABERTURA) {
    digitalWrite(led2, LOW);
    noTone(buzzer);
    servo2.write(0);
    cancelaAbertaSaida = false;
    carroSaidaProcessado = false;
  }
}

void loop() {
  distanciaEntrada = medirDistancia(trig1, echo1);
  delay(50);
  distanciaSaida = medirDistancia(trig2, echo2);
  controlarEntrada();
  controlarSaida();

  lcd_1.setCursor(0, 0);
  lcd_1.print("Vagas livres:");
  lcd_1.setCursor(13, 0);
  lcd_1.print("  ");
  lcd_1.setCursor(13, 0);
  lcd_1.print(vagasDisponiveis);

  delay(200);
}
